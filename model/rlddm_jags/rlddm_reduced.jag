model {
ag_mu    ~  dunif(0.5,3)
ag_sd    ~  dunif(0.001,5)
ag_tau   <- pow(ag_sd,-2) 
etag_mu  ~  dunif(-5,5) 
etag_sd  ~  dunif(0.001,5)
etag_tau <- pow(etag_sd,-2)
ig_mu    ~  dunif(-0.5,0.5)
ig_sd    ~  dunif(0.001,5)
ig_tau   <- pow(ig_sd,-2)
mg_mu    ~  dunif(0,10)
mg_sd    ~  dunif(0.001,5)
mg_tau   <- pow(mg_sd,-2)
tg_mu    ~  dunif(0.05,1)
tg_sd    ~  dunif(0.001,5)
tg_tau   <- pow(tg_sd,-2 )
for (v in 1:2){
     etag[v]     ~  dnorm(etag_mu,etag_tau)
     eta_sd[v]  ~  dunif(0.001, 5)
     eta_tau[v]  <- pow(eta_sd[v],-2)
}
ag      ~  dnorm(ag_mu,ag_tau)
a_sd    ~  dunif(0.001, 5)
a_tau   <- pow(a_sd,-2) 
ig      ~  dnorm(ig_mu,ig_tau)
i_sd    ~  dunif(0.001, 5)
i_tau   <- pow(i_sd,-2)
mg      ~  dnorm(mg_mu,mg_tau)
m_sd    ~  dunif(0.001, 5)
m_tau   <- pow(m_sd,-2)
tg      ~  dnorm(tg_mu,tg_tau)
t_sd    ~  dunif(0.001, 5)
t_tau   <- pow(t_sd,-2)
for (s in 1:S) {
     for(v in 1:2){
          eta[s,v] ~ dnorm(etag[v], eta_tau[v])
     }
     a[s]   ~ dnorm(ag,a_tau)
     i[s]   ~ dnorm(ig,i_tau)
     m[s]   ~ dnorm(mg,m_tau)
     t[s]   ~ dnorm(tg,t_tau)
}
for (s in 1:S) {
     ev[first[s],1] <- 0
     ev[first[s],2] <- 0
     for (trial in (first[s]):(last[s]-1)) {
          v[trial] <- (ev[trial,1] - ev[trial,2]) * (m[s])
          RT[trial] ~ dwiener(a[s] * pow(iter[trial]/10,i[s]),t[s] ,0.5,v[trial])
          log_lik[trial] <- logdensity.wiener(RT[trial], a[s] * pow(iter[trial]/10,i[s]),t[s],0.5,v[trial])
          ev[trial+1,correct[trial]] <- ev[trial,correct[trial]] + ilogit(eta[s,correct[trial]] * (value[trial]-ev[trial,correct[trial]]))
          ### adjust ev for incorrect
          ev[trial+1,incorrect[trial]] <- ev[trial,incorrect[trial]] + ilogit(eta[s,incorrect[trial]] * (value[trial]-ev[trial,incorrect[trial]]))
          }
     for (trial in last[s]) {
          v[trial] <- (ev[trial,1] - ev[trial,2]) * m[s]
          RT[trial] ~ dwiener(a[s] * pow(iter[trial]/10,i[s]),t[s],0.5,v[trial])
          log_lik[trial] <- logdensity.wiener(RT[trial], a[s]  * pow(iter[trial]/10,i[s]),t[s],0.5,v[trial])
          }
     }
}